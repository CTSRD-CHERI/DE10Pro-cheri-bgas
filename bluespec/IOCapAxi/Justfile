default:
    @just --list

gen-bitfields:
    #!/bin/bash
    python3 ./testbenches/generate_bitfield.py > ./testbenches/include/tb_bitfields.h

sim-b name:
    make ./build/{{name}}.bsim
    ./build/{{name}}.bsim

build-v name:
    make ./build/verilog/{{name}}.v

build-sim-v name:
    make ./build/{{name}}.vsim

sim-v name *args:
    make ./build/{{name}}.vsim
    ./build/{{name}}.vsim --toml ./testbenches/results/{{name}}.toml {{args}}

test-cpp-encdec:
    #!/bin/bash
    clang++ -g -std=gnu++20 -I$(dirname $(which verilator))/../share/verilator/include -I./testbenches/include -I./testbenches/librust_caps_c ./testbenches/librust_caps_c/librust_caps_c.a ./testbenches/checkEncDec.cpp -o ./build/checkEncDec
    ./build/checkEncDec

test-keymgr *args: (sim-v "mkSimpleIOCapKeyManager_Tb" args)

test-exposer-v1 *args: (sim-v "mkSimpleIOCapExposerV1_Tb" args)
test-exposer-v2 *args: (sim-v "mkSimpleIOCapExposerV2_Tb" args)
test-exposer-v3 *args: (sim-v "mkSimpleIOCapExposerV3_Tb" args)
test-exposer-v4-noblock *args: (sim-v "mkSimpleIOCapExposerV4_noblock_Tb" args)
test-exposer-v4 *args: (sim-v "mkSimpleIOCapExposerV4_blockinvalid_Tb" args)
test-exposer-v5-noblock *args: (sim-v "mkSimpleIOCapExposerV5_noblock_Tb" args)
test-exposer-v5 *args: (sim-v "mkSimpleIOCapExposerV5_blockinvalid_Tb" args)
test-exposer-v6 *args: (sim-v "mkSimpleIOCapExposerV6_blockinvalid_Tb" args)
test-exposer-v6-1per *args: (sim-v "mkSimpleIOCapExposerV6_blockinvalid_1percycle_Tb" args)
test-decoder-comb-11 *args: (sim-v "mkCap2024_11_Decode_Comb_Tb" args)
test-decoder-comb-02 *args: (sim-v "mkCap2024_02_Decode_Comb_Tb" args)
test-decoder-fsm-11 *args: (sim-v "mkCap2024_11_Decode_FastFSM_Tb" args)
test-decoder-fsm-02 *args: (sim-v "mkCap2024_02_Decode_FastFSM_Tb" args)

test-keymgr2-all: test-keymgr2-keydata-dualportsingle

test-keymgr2-keydata-dualportsingle *args: (sim-v "mkIOCapAxi_KeyManager2_KeyDataPipe_DualPortSingleCheckerPort_Tb" args)
test-keymgr2-refcount-twovalve *args: (sim-v "mkIOCapAxi_KeyManager2_RefCountPipe_TwoValve_Tb" args)

test-combined-v6-2v1 *args: (sim-v "mkCombinedIOCapExposerV6_blockinvalid_1percycle_KeyManager2V1_Tb" args)

build-all: \
    (build-sim-v "mkSimpleIOCapKeyManager_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV1_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV2_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV3_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV4_noblock_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV4_blockinvalid_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV5_noblock_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV5_blockinvalid_Tb") \
    (build-sim-v "mkSimpleIOCapExposerV6_blockinvalid_Tb") \
    (build-sim-v "mkCap2024_11_Decode_Comb_Tb") \
    (build-sim-v "mkCap2024_02_Decode_Comb_Tb") \
    (build-sim-v "mkCap2024_11_Decode_FastFSM_Tb") \
    (build-sim-v "mkCap2024_02_Decode_FastFSM_Tb") \
    (build-sim-v "mkIOCapAxi_KeyManager2_KeyDataPipe_DualPortSingleCheckerPort_Tb") \
    (build-sim-v "mkIOCapAxi_KeyManager2_RefCountPipe_TwoValve_Tb") \
    (build-sim-v "mkCombinedIOCapExposerV5_blockinvalid_KeyManager2V1_Tb")

konatify name pattern *args:
    just {{name}} --konata {{args}} | python sanitize_konata.py ./testbenches/results/konata/{{name}} --pattern "{{pattern}}"